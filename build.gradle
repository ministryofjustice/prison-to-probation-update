import java.time.Instant
import java.time.LocalDate

plugins {
  id 'java'
  id 'org.jetbrains.kotlin.jvm' version '1.3.72'
  id 'org.springframework.boot' version '2.2.6.RELEASE'
  id 'org.jetbrains.kotlin.plugin.spring' version '1.3.72'
  id 'io.spring.dependency-management' version '1.0.9.RELEASE'
  id 'org.owasp.dependencycheck' version '5.3.2.1'
  id 'com.github.ben-manes.versions' version '0.28.0'
  id 'se.patrikerdes.use-latest-versions' version '0.2.13'
}

repositories {
  mavenLocal()
  mavenCentral()
}

compileKotlin {
  kotlinOptions.jvmTarget = '11'
}

compileTestKotlin {
  kotlinOptions.jvmTarget = '11'
}

configurations {
    agentDeps
    agentDeps.transitive = false
}

project.sourceCompatibility = 11
project.targetCompatibility = 11

dependencyCheck {
  failBuildOnCVSS = 5
  suppressionFiles = ['dependency-check-suppress-spring.xml']
  format = 'ALL'
  analyzers {
    assemblyEnabled = false
  }
}

def isNonStable = { String version ->
  def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
  def regex = /^[0-9,.v-]+(-r)?$/
  return !stableKeyword && !(version ==~ regex)
}

dependencyUpdates {
  rejectVersionIf {
    isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
  }
}

group = 'uk.gov.justice.digital.hmpps'

def todaysDate = LocalDate.now().format('yyyy-MM-dd')
version = System.getenv().containsKey("BUILD_NUMBER") ? System.getenv("BUILD_NUMBER") : todaysDate

springBoot {
  buildInfo {
    properties {
      artifact = rootProject.name
      version
      group
      name = rootProject.name
      time = Instant.now()
      additional = [
              by                   : System.properties['user.name'],
              operatingSystem      : "${System.properties['os.name']} (${System.properties['os.version']})",
              machine              : InetAddress.localHost.hostName
      ]
    }
  }
}

configurations {
  implementation.exclude module: 'tomcat-jdbc'
}

dependencyManagement {
  imports {
    mavenBom(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)
  }
}

ext ['spring-security.version'] = '5.3.1.RELEASE' // pinned due to CVE-2018-1258 in 5.2.2.RELEASE brought in by spring-boot-starter-oauth2-resource-server-2.2.5.RELEASE

dependencies {
  annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

  implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
  implementation 'org.jetbrains.kotlin:kotlin-reflect'

  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
  implementation 'org.springframework.boot:spring-boot-devtools'

  implementation("org.springframework.security:spring-security-oauth2-jose")
  implementation("org.springframework:spring-webflux")
  implementation("org.springframework.boot:spring-boot-starter-oauth2-client")
  implementation("org.springframework.boot:spring-boot-starter-reactor-netty")
  
  implementation 'net.logstash.logback:logstash-logback-encoder:6.3'
  implementation 'com.microsoft.azure:applicationinsights-spring-boot-starter:2.6.0'
  implementation 'com.microsoft.azure:applicationinsights-logging-logback:2.6.0'
  agentDeps 'com.microsoft.azure:applicationinsights-agent:2.6.0'
  implementation configurations.agentDeps
  implementation 'com.github.timpeeters:spring-boot-graceful-shutdown:2.2.1'
  implementation 'net.javacrumbs.shedlock:shedlock-spring:4.8.0'
  implementation 'net.javacrumbs.shedlock:shedlock-provider-dynamodb:4.8.0'

  implementation 'javax.transaction:javax.transaction-api:1.3'
  implementation 'javax.xml.bind:jaxb-api:2.3.1'
  implementation 'com.sun.xml.bind:jaxb-impl:2.3.2'
  implementation 'com.sun.xml.bind:jaxb-core:2.3.0.1'
  implementation 'com.google.code.gson:gson:2.8.6'

  implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.10.3'
  implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.10.3'

  implementation 'org.springframework:spring-jms'
  implementation platform('com.amazonaws:aws-java-sdk-bom:1.11.764')
  implementation 'com.amazonaws:amazon-sqs-java-messaging-lib:1.0.8'
  implementation 'com.amazonaws:aws-java-sdk-dynamodb:1.11.764'
  implementation 'io.github.boostchicken:spring-data-dynamodb:5.2.3'

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.security:spring-security-test'
  testImplementation 'org.springframework.boot:spring-boot-starter-webflux'
  testImplementation 'com.github.tomakehurst:wiremock-standalone:2.26.3'
  testImplementation 'net.javacrumbs.json-unit:json-unit-assertj:2.17.0'
  testImplementation 'com.nhaarman:mockito-kotlin-kt1.1:1.6.0'
  testImplementation "org.testcontainers:localstack:1.13.0"
  testImplementation "org.awaitility:awaitility-kotlin:4.0.2"
}

task copyAgent(type: Copy) {
  from configurations.agentDeps
  into "$buildDir/libs"
}

assemble.dependsOn copyAgent

test {
  useJUnitPlatform()
}
